axiom juanEsBajo : bajo(juan)

theorem noTodoElMundoEsAlto : ~forall X. ~bajo(X)
proof
  suppose todosSonAltos: forall X. ~bajo(X)
  thus false by juanEsBajo, todosSonAltos
end

// Razonamiento por el absurdo con eliminación de doble negación
theorem hayAlguienBajo_DNegE : exists X. bajo(X)
proof
    equivalently ~~exists X. bajo(X)
    suppose h1: ~exists X. bajo(X)

    claim h2: forall X. ~bajo(X)
    proof
        let X
        suppose h3: bajo(X)
        claim h4: exists Y. bajo(Y)
        proof
            take Y := X
            hence bajo(X) by h3
        end
        hence false by h1
    end

    hence false by noTodoElMundoEsAlto
end

// Razonamiento por el absurdo mediante LEM
theorem hayAlguienBajo_LEM : exists X. bajo(X)
proof
  cases
    case exists X. bajo(X)
      hence exists X. bajo(X)
    case h1: ~exists X. bajo(X)
      claim h2: forall X. ~bajo(X)
      proof
        let X
        cases
          case ~bajo(X)
            hence ~bajo(X)
          case h3: bajo(X)
            claim h4: exists Y. bajo(Y)
            proof
              take Y := X
              hence bajo(X) by h3
            end
            have -: false by h1, h4
            hence ~bajo(X)
        end
      end
      have -: false by h2, noTodoElMundoEsAlto
      hence exists X. bajo(X)
  end
end